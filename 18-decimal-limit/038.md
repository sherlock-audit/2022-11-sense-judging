ctf_sec

medium

# Lack of support for token that is not 18 decimals.

## Summary

Lack of support for token that is not different decimals 

## Vulnerability Detail

this function totalAssets is very important because it is used to determine how much shares minted to user and how much token amount users can redeem. In Solmate ERC4626 implementation:

```solidity
function totalAssets() public view virtual returns (uint256);

function convertToShares(uint256 assets) public view virtual returns (uint256) {
	uint256 supply = totalSupply; // Saves an extra SLOAD if totalSupply is non-zero.

	return supply == 0 ? assets : assets.mulDivDown(supply, totalAssets());
}

function convertToAssets(uint256 shares) public view virtual returns (uint256) {
	uint256 supply = totalSupply; // Saves an extra SLOAD if totalSupply is non-zero.

	return supply == 0 ? shares : shares.mulDivDown(totalAssets(), supply);
}
```

let us look into the totalAssets() implementation. We can immediately say if the token decimials is not 18, the totalAssets() would have issue.

```solidity
/// @notice Calculates the total assets of this vault using the current spot prices, with no regard for slippage.
function totalAssets() public view override returns (uint256) {
	if (maturity == MATURITY_NOT_SET) {
		return asset.balanceOf(address(this));
	} 
	else {
		Space _space = space;
		(uint256 ptReserves, uint256 targetReserves) = _getSpaceReserves();

		(uint256 targetBal, uint256 ptBal, uint256 ytBal, ) = _decomposeShares(ptReserves, targetReserves, totalSupply, true);

		uint256 ptSpotPrice = _space.getPriceFromImpliedRate(
			(ptReserves + _space.adjustedTotalSupply()).divWadDown(targetReserves.mulWadDown(initScale)) - 1e18
		); // PT price in Target.

		uint256 scale = adapter.scaleStored();

		if (ptBal >= ytBal) {
			// Target + combined PTs/YTs + PT spot value in Target.
			return targetBal + ytBal.divWadDown(scale) + ptSpotPrice.mulWadDown(ptBal - ytBal);
		} else {
			uint256 ytSpotPrice = (1e18 - ptSpotPrice.mulWadDown(scale)).divWadDown(scale);

			// Target + combined PTs/YTs + YT spot value in Target.
			return targetBal + ptBal.divWadDown(scale) + ytSpotPrice.mulWadDown(ytBal - ptBal);
		}
	}
}
```

the first case return the balanceOf in the token asset's own precision, maybe 18, maybe not 18 decimals.

Some tokens have low decimals (e.g. USDC has 6). Even more extreme, some tokens like Gemini USD only have 2 decimals.
USDT has decimal also has 6 decimals. The USDC / USDT adoption is wide enough to not consider these non-18 decimals token as edge case.

let us just use USDC as an example and let us say the asset token has 6 decimals.

```solidity
if (maturity == MATURITY_NOT_SET) {
	return asset.balanceOf(address(this));
}
```

the else part, assume that the spot price is 18 deciimals.

let us focus two lines of code:

```solidity
(uint256 ptReserves, uint256 targetReserves) = _getSpaceReserves();
```

which calls

```solidity
/// @dev Get PT and Target reserve balances for the current Space pool.
/// @return ptReserves PT reserve amount.
/// @return targetReserves Target reserve amount.
function _getSpaceReserves() internal view returns (uint256, uint256) {
	(, uint256[] memory balances, ) = balancerVault.getPoolTokens(poolId);
	uint256 _pti = pti;
	return (balances[_pti], balances[1 - _pti]);
}
```

if we check the balancer doc.

https://dev.balancer.fi/references/contracts/apis/the-vault#getpooltokens

```solidity
getPoolTokens(bytes32 poolId)
returns (IERC20[] tokens, 
    uint256[] balances,
    uint256 lastChangeBlock)
```

Returns a Pool's registered tokens, the total balance for each, and the most recent block in which any of the tokens were updated. Implemented by PoolAssets. Implemented in PoolTokens.

the balances is the total balance for each, no decimal check and decimal normalization here.

let us focus on this line of code:

```solidity
_space.getPriceFromImpliedRate
```

we calls.

https://github.com/sense-finance/space-v1/blob/e9388f8ae5fbeff0433dfb48fe0ee35df20e71c6/src/Space.sol#L707

```solidity
/// @notice Get price of PTs in Target terms given a price for PTs in Target
/// @param impliedRate Normed implied rate
function getPriceFromImpliedRate(uint256 impliedRate) public view returns (uint256 pTPriceInTarget) {
	if (block.timestamp >= maturity) {
		return FixedPoint.ONE;
	}

	// Calculate the PT price in Target from an implied rate adjusted by the timeshift param,
	// where the timeshift is a normalization factor applied to the time to maturity
	pTPriceInTarget = FixedPoint.ONE
		.divDown(impliedRate.add(FixedPoint.ONE)
		.powDown(((maturity - block.timestamp) * FixedPoint.ONE)
		.divDown(FixedPoint.ONE.divDown(ts))))
		.divDown(AdapterLike(adapter).scaleStored());
}
```

FixedPoint.one is 10e18, we always compute the price in 18 decimals.

the FixedPoint imports from Space implementation is

```solidity
import { FixedPoint } from "@balancer-labs/v2-solidity-utils/contracts/math/FixedPoint.sol";
```

which is

https://github.com/balancer-labs/balancer-v2-monorepo/blob/d24834f37d1d167f44ac7b410b5a786904834aec/pkg/solidity-utils/contracts/math/FixedPoint.sol#L26

```solidity
library FixedPoint {
    // solhint-disable no-inline-assembly

    uint256 internal constant ONE = 1e18; // 18 decimal places
```


## Impact

Impact is severe, let us still some USDC as the asset.

then we have

```solidity
function totalAssets() public view override returns (uint256) {
	if (maturity == MATURITY_NOT_SET) {
		return asset.balanceOf(address(this)); // 6 decimals
	} 
	else {
	  return price // 18 decimals.
	}
```

given this code:

```solidity
function convertToShares(uint256 assets) public view virtual returns (uint256) {
	uint256 supply = totalSupply; // Saves an extra SLOAD if totalSupply is non-zero.

	return supply == 0 ? assets : assets.mulDivDown(supply, totalAssets());
}
```

when the maturity is not set, the totalAssets would be much smaller (amount in 6 decimals is much more smaller than amount 18 decimals).

Then the user can mint large large amount of share when the maturity is not set because according to the formula:assets * supply / totalAssets(),

if totalAssets() is small, more share is minted.


## Code Snippet

https://github.com/sherlock-audit/2022-11-sense/blob/main/contracts/src/AutoRoller.sol#L387-L416

https://github.com/sherlock-audit/2022-11-sense/blob/main/contracts/src/AutoRoller.sol#L757-L766

## Tool used

Manual Review

## Recommendation

We recommend the project add check to make sure the asset token decimal is 18.